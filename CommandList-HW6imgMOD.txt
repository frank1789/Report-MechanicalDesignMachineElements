!*********************
! PROBLEM HOMEWORK 6 *
!*********************

FINISH
/CLEAR,START,NEW
/TITLE,HOMEWORK 6
/FILNAME,HOMEWORK6
! >>> PARAMETERS MODEL <<<
*SET,l,50/2
*SET,g,2
*SET,t,1.5
*SET,Wp,25/2
*SET,Rp,4
*SET,Rd,3
*SET,delta,4*t
*SET,EPS,1E-3
*SET,n_div,12
*SET,e_lenght,1
*SET,step,100
*SET,x,delta/step

! >>> PROPERTIES MATERIAL <<<
*SET,E_Young,205E3
*SET,ni,0.3
*SET,SigmaYield,355
*SET,E_p,4E3
*SET,friction,0.1

! >>> LOAD <<<
*SET,F,100E3

! >>> PRE PROCESSING <<<
/PREP7
ET,1,PLANE183,,,1

MP,EX,1,E_Young
MP,PRXY,1,ni
MP,MU,1,friction
TB,BKIN,1,1
TBTEMP,0.0
TBDATA,1,SigmaYield,E_p

!***PUNCH MODEL
K,1,0,0
K,2,Wp,0
K,3,Wp,Wp
K,4,0,Wp
L,1,2
L,2,3
L,3,4
L,4,1
LFILLT,1,2,Rp
AL,1,2,3,4,5

!***BLANCK HOLDER MODEL
K,7,Wp+g,0
K,8,Wp+g+(l/4),0
K,9,l+g,0
K,10,l+g,Wp/2
K,11,Wp+g,Wp/2
L,7,8
L,8,9
L,9,10
L,10,11
L,11,7
AL,6,7,8,9,10

!***DIE MODEL
K,12,Wp+g,-t
K,13,Wp+g+(l/4),-t
K,14,l+g,-t
K,15,l+g,-t-Wp
K,16,Wp+g,-t-Wp
L,12,13
L,13,14
L,14,15
L,15,16
L,16,12
LFILLT,15,11,Rd
AL,11,12,13,14,15,16
K,19,0,0
K,20,l,0
K,21,l,-t
K,22,0,-t
L,19,20
L,20,21
L,21,22
L,22,19
AL,17,18,19,20
SAVE

! >>> MESHING <<<
LESIZE,18,,,n_div
LESIZE,20,,,n_div

ESIZE,e_lenght
MSHAPE,0
MSHKEY,1
AMESH,4
SAVE

! >>> DEFINITION CONTACT <<<
ET,2,TARGE169  
ET,3,CONTA172
KEYOPT,3,2,0
KEYOPT,3,7,1  
KEYOPT,3,9,2	  
KEYOPT,3,10,2

!****PUNCH - BLANK
!***DEFORMABLE BLANK
TYPE,3
REAL,1
LSEL,s,LINE,,17
NSLL,s,1
NSEL,R,LOC,x,0,Wp+g
ESURF
ALLSEL,all
SAVE

!***RIGIDBODY PUNCH
TYPE,2
REAL,1
TSHAP,LINE
LMESH,1
TSHAP,ARC
LMESH,5
SAVE
TSHAP,PILO 
KMESH,1

!***CONTACT BLANKHOLDER-BLANK
TYPE,3
REAL,2
LSEL,s,LINE,,17
NSLL,s,1
NSEL,R,LOC,x,Wp+g,l+g
ESURF
ALLSEL,all
SAVE

TYPE,2
REAL,2
TSHAP,LINE
LMESH,6,7
TSHAP,PILO
KMESH,8
SAVE

!***CONTACT DIE-BLANK
TYPE,3
REAL,3
LSEL,S,LINE,,19
NSLL,S,1
NSEL,R,LOC,x,Wp,l
ESURF
ALLSEL,all
SAVE

TYPE,2
REAL,3
TSHAP,LINE
LMESH,11,12
LSEL,S,LINE,,11,12
ESLL,S
ESURF,,REVERSE
ALLSEL,ALL
SAVE

TSHAP,ARC
LMESH,16
ESEL,S,,,345
ESURF,,REVERSE  
ALLSEL,ALL
TSHAP,PILO
KMESH,13
SAVE

! >>>CONSTRAIN <<<
LSEL,S,,,20
NSLL,S
DSYM,SYMM,X
ALLSEL,ALL,ALL
SAVE

! >>> LOAD CONDITION <<<
!***LOAD STEP 1: PRELOAD
TIME,1 
F,981,fy,-F 
OUTRES,ALL,ALL 
LSWRITE,1
!***LOAD STEP 2: DESCENT OF PUNCH
*DO,i,1,step
 TIME,1+i
 D,976,uy,-x*i
 OUTRES,ALL,ALL 
 LSWRITE,1+i 
*ENDDO
!***LOAD STEP 3: ASCENT OF PUNCH
*DO,i,step+2,step*2
 TIME,i 
 D,976,uy,(-x*step)+(i-step)*x
 OUTRES,ALL,ALL
 LSWRITE,i
*ENDDO
SAVE

! >>> SOLUTION <<<
/SOLU
SOLCONTROL,ON
NSUBST,10,100,2	 
NROPT,FULL,,OFF	 
AUTOTS,ON
EQSLV,PCG
NLGEOM,ON
LSSOLVE,1,2*step
FINISH

! >>> POST PROCESSING <<<
/POST1
*DO,N,1,2*step
 SET,N
 PLNSOL,S,EQV
*ENDDO

!***VON MISES EQUIVALENT STRESS
!END OF THE TRAVEL OF THE PUNCH
SET,step+1
PLNSOL,S,EQV 
PRRFOR,FY
*DO,N,1,2*step
 SET,N
 *GET,F,NODE,976,RF,FY
 *CFOPEN,ForcePunch,txt,,APPEND 
 *VWRITE,N,F
 (F20.10,F20.10)
 *CFCLOS
*ENDDO

!***PUNCH STROKE 
!THE MAXIMUM ABSOLUTE HOOP STRAIN BELOW 5%.
*DO,N,1,2*step
 SET,N
 PLNSOL,EPTO,Z
 *GET,MAXSTR,PLNSOL,0,MAX
 *GET,MINSTR,PLNSOL,0,MIN
 *CFOPEN,HoopStrain,txt,,APPEND
 *VWRITE,N,MAXSTR,MINSTR
 (F20.10,F20.10,F20.10)
 *CFCLOS
*ENDDO
SAVE

!***RESIDUAL STRESS DISTRIBUTION SURFACE
SET,103
PATH,TOP,2,,1000
PPATH,1,,0,0
PPATH,2,,L,0 
PDEF,TOP_SEQV,S,EQV
PLPATH,TOP_SEQV

!***RESIDUAL STRESS 
!DISTRIBUTION BOTTOM SURFACE
PATH,BOTTOM,2,,1000
PPATH,1,,0,-T
PPATH,2,,L,-T
PDEF,BOT_SEQV,S,EQV
PLPATH,BOT_SEQV 

!***AXIAL DISPLACEMENT AFTER THE PUNCH REMOVE
SET,step+1
NSEL,S,LOC,y,-(1.5/2)-0.05,-(1.5/2)+0.05
*GET,nnodi,NODE,,count
*DIM,dy,ARRAY,nnodi
*DIM,xn,ARRAY,nnodi 
CM,nodi,NODE
*do,i,1,nnodi
 *GET,xmin,NODE,,mnloc,x
 nsel,r,loc,x,xmin-EPS,xmin+EPS
 *GET,nmin,NODE,,NUM,MAX 
 xn(i)=xmin 
 *GET,dy(i),NODE,nmin,u,y
 CMSEL,S,nodi
 NSEL,U,NODE,,nmin
 CM,nodi,NODE
 *CFOPEN,set%step+1%,txt
 *VWRITE,xn(1),dy(1)
 (F20.10,F20.10)
 *CFCLOS
*ENDDO 
*DEL,ALL

SET,103
NSEL,S,LOC,y,-(1.5/2)-0.05,-(1.5/2)+0.05
*GET,nnodi,NODE,,COUNT
*DIM,dy,ARRAY,nnodi
*DIM,xn,ARRAY,nnodi 
CM,NODI,NODE
*DO,i,1,nnodi
 *GET,XMIN,NODE,,MNLOC,X
 NSEL,R,LOC,X,xmin-EPS,xmin+EPS
 *GET,NMIN,NODE,,NUM,MAX 
 xn(i)=XMIN
 *GET,DY(i),NODE,NMIN,U,Y
 CMSEL,S,NODI
 NSEL,U,NODE,,NMIN
 CM,NODI,NODE
 *CFOPEN,set103,txt
 *VWRITE,xn(1),dy(1)
 (F20.10,F20.10) 
 *CFCLOS
*ENDDO
FINISH